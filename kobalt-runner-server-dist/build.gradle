apply plugin: 'com.github.rodm.teamcity-server'

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
    serverOptions = project.findProperty('server.options') ?: '-Dteamcity.development.mode=true -Dteamcity.development.shadowCopyClasses=true'
    agentOptions = project.findProperty('agent.options') ?: ''
}

dependencies {
    agent project(path: ':kobalt-runner-agent-dist', configuration: 'plugin')
    server project(':kobalt-runner-common')
    server project(':kobalt-runner-server')
}

def libFiles = {
    project(':kobalt-runner-server').configurations.compile.files({
        it.group == 'org.jetbrains.kotlin' && it.name == 'kotlin-stdlib' ||
        it.group == 'com.github.kittinunf.fuel' && it.name == 'fuel' ||
        it.group == 'com.google.code.gson' && it.name == 'gson' ||
        it.group == 'com.github.kittinunf.result' && it.name == 'result'
    })
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = 'Kobalt Runner'
            displayName = 'Kobalt Runner'
            version = project.version
            vendorName = 'Dmitry Zhuravlev'
            description = 'TeamCity build runner for Kobalt build system'
            email = 'dmitry.anatolyevich.zhuravlev@gmail.com'
            useSeparateClassloader = true
        }

        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        files {
            into('server') {
                from libFiles()
            }
        }

        environments {
            teamcity10 {
                version = '10.0.2'
                dataDir = file("$rootDir/servers/TeamCity-$version/data")
                javaHome = file(java8Home)
                serverOptions = project.serverOptions
                agentOptions = project.agentOptions
            }
        }
    }
}
